# web application load balancer
resource "aws_lb" "web_alb" {
  name               = "web-alb"
  internal           = false
  load_balancer_type = "application"

  security_groups = [var.frontend_alb_sg_id]
  subnets         = var.public_subnets

  enable_deletion_protection = false
  idle_timeout               = 60

  tags = {
    Name        = "web_alb_${terraform.workspace}"
    Environment = "${terraform.workspace}"
    Project     = "vpc-alb"
    Tier        = "frontend"
  }
}

resource "aws_lb_target_group" "web" {
  name     = "web"
  port     = 80
  protocol = "HTTP"
  vpc_id   = var.vpc_id

  health_check {
    enabled             = true
    healthy_threshold   = 2
    interval            = 30
    matcher             = "200"
    path                = "/"
    port                = "traffic-port"
    protocol            = "HTTP"
    timeout             = 5
    unhealthy_threshold = 2

  }

  tags = {
    Name        = "web_tg_${terraform.workspace}"
    Environment = "${terraform.workspace}"
    Project     = "vpc-alb"
    Tier        = "frontend"
  }
}


# web launch template
resource "aws_launch_template" "web" {
  name_prefix = "${terraform.workspace}_web"

  image_id      = var.image_id
  instance_type = var.web_instance_type

  user_data = var.web_user_data_base64




  tag_specifications {
    resource_type = "instance"
    tags = {
      Name        = "web_${terraform.workspace}"
      Environment = "${terraform.workspace}"
      Project     = "vpc-alb"
      Tier        = "frontend"
    }
  }

}


resource "aws_autoscaling_group" "web" {
  name_prefix = "${terraform.workspace}_web"

  vpc_zone_identifier = var.web_private_subnets

  desired_capacity = 2

  min_size = 2
  max_size = 4

  launch_template {
    id      = aws_launch_template.web.id
    version = "$Latest"
  }

  target_group_arns = [aws_lb_target_group.web.id]

  tag {
    key                 = "Name"
    value               = "web_${terraform.workspace}"
    propagate_at_launch = true
  }
  tag {
    key                 = "Environment"
    value               = terraform.workspace
    propagate_at_launch = true
  }
  tag {
    key                 = "Project"
    value               = "vpc-alb"
    propagate_at_launch = true
  }
  tag {
    key                 = "Tier"
    value               = "frontend"
    propagate_at_launch = true
  }

}


# App ALB (Internal)

resource "aws_lb" "app_alb" {
  name               = "app-alb"
  internal           = true
  load_balancer_type = "application"

  security_groups = [var.backend_alb_sg_id]
  subnets         = var.public_subnets

  enable_deletion_protection = false
  idle_timeout               = 60

  tags = {
    Name        = "app_alb_${terraform.workspace}"
    Environment = "${terraform.workspace}"
    Project     = "vpc-alb"
    Tier        = "backend"
  }
}


resource "aws_lb_target_group" "app" {
  name     = "app"
  port     = 80
  protocol = "HTTP"
  vpc_id   = var.vpc_id

  health_check {
    enabled             = true
    healthy_threshold   = 2
    interval            = 30
    matcher             = "200"
    path                = "/"
    port                = "traffic-port"
    protocol            = "HTTP"
    timeout             = 5
    unhealthy_threshold = 2

  }

  tags = {
    Name        = "app_tg_${terraform.workspace}"
    Environment = "${terraform.workspace}"
    Project     = "vpc-alb"
    Tier        = "backend"
  }
}


resource "aws_lb_listener" "app" {
  load_balancer_arn = aws_lb.app_alb.id
  port              = 80
  protocol          = "HTTP"

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.app.id
  }

}

# app launch template and asg

# App IAM Role
resource "aws_iam_role" "app_role" {
  name = "app_role_${terraform.workspace}"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          Service = "ec2.amazonaws.com"
        }
      }
    ]
  })
}

resource "aws_iam_policy" "secrets_policy" {
  name        = "secrets_policy_${terraform.workspace}"
  description = "Allow access to secrets manager"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = [
          "secretsmanager:GetSecretValue"
        ]
        Effect   = "Allow"
        Resource = var.secret_arn
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "secrets_attach" {
  role       = aws_iam_role.app_role.name
  policy_arn = aws_iam_policy.secrets_policy.arn
}

resource "aws_iam_instance_profile" "app_profile" {
  name = "app_profile_${terraform.workspace}"
  role = aws_iam_role.app_role.name
}

resource "aws_launch_template" "app" {
  name_prefix = "${terraform.workspace}_app"

  image_id      = var.image_id
  instance_type = var.app_instance_type

  user_data = var.app_user_data_base64

  iam_instance_profile {
    name = aws_iam_instance_profile.app_profile.name
  }



  tag_specifications {
    resource_type = "instance"
    tags = {
      Name        = "app_${terraform.workspace}"
      Environment = "${terraform.workspace}"
      Project     = "vpc-alb"
      Tier        = "backend"
    }
  }



}

resource "aws_autoscaling_group" "app" {
  name_prefix = "${terraform.workspace}_app"

  vpc_zone_identifier = var.app_private_subnets

  desired_capacity = 2

  min_size = 2
  max_size = 4

  launch_template {
    id      = aws_launch_template.app.id
    version = "$Latest"
  }

  target_group_arns = [aws_lb_target_group.app.id]

  tag {
    key                 = "Name"
    value               = "app_${terraform.workspace}"
    propagate_at_launch = true
  }
  tag {
    key                 = "Environment"
    value               = terraform.workspace
    propagate_at_launch = true
  }
  tag {
    key                 = "Project"
    value               = "vpc-alb"
    propagate_at_launch = true
  }
  tag {
    key                 = "Tier"
    value               = "backend"
    propagate_at_launch = true
  }


}
